  //%attributes = {"invisible":true,"lang":"en"} comment added and reserved by 4D.
  // --[HEADERSTART]-------------------------------------
  // User name (OS): Joshua Fletcher
  // Date and time: 07/18/14, 18:10:02
  // ----------------------------------------------------
  // Description:
  //   Replace old preference key with new one.
  //
  //   Preserve old value if it exists.
  // --[HEADEREND]---------------------------------------
C_TEXT($1;$oldLabel_t)
C_TEXT($2;$newLabel_t)

C_LONGINT($0;$error_l)

C_TEXT($oldValue_t)

$oldLabel_t:=$1
$newLabel_t:=$2

  // Open external database.
$error_l:=VC_EXT_Open 

  // Get the old value.
If ($error_l=0)
	$error_l:=VC_EXT_PrefGet ($oldLabel_t;->$oldValue_t)
End if 

Case of 
	: ($error_l=<>ERR_PREF_NotFound_l)
		  // It was already removed. We're done here.
		$error_l:=0
		
		
	: ($error_l=0)
		  // Got the old value, delete the old preference.
		$error_l:=VC_EXT_PrefDelete ($oldLabel_t)
		
		If ($error_l=0)
			  // Preserve the old value.
			$error_l:=VC_EXT_PrefSet ($newLabel_t;$oldValue_t)
		End if 
		
End case 

  // Close external database.
$error_l:=VC_EXT_Close ($error_l)

If ($error_l#0)
	APPEND TO ARRAY(UTIL_ERR_CallChain_at;Current method name)
End if 

$0:=$error_l

