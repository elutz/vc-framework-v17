  //%attributes = {"invisible":true,"lang":"en"} comment added and reserved by 4D.
  // --[HEADERSTART]-------------------------------------
  // User name (OS): Joshua Fletcher
  // Date and time: 07/11/14, 15:16:56
  // ----------------------------------------------------
  // Description:
  //   Wrapper to create table in external database.
  //
  //   You need to pass a valid SQL table definition.
  //
  //   I did this just to make the calling code more
  //   readable.
  // --[HEADEREND]---------------------------------------
C_TEXT($1;$tableDef_t)
C_LONGINT($0;$error_l)

C_TEXT($createTableStatement_t)

$tableDef_t:=$1

$createTableStatement_t:=$createTableStatement_t+"CREATE TABLE IF NOT EXISTS "
$createTableStatement_t:=$createTableStatement_t+$tableDef_t
$createTableStatement_t:=$createTableStatement_t+";"

$error_l:=VC_EXT_Open 

  // Create preferences table.
If ($error_l=0)
	UTIL_ERR_HandlerInstall 
	Begin SQL
		EXECUTE IMMEDIATE :$createTableStatement_t;
	End SQL
	$error_l:=UTIL_ERR_HandlerRemove 
End if 


  // Close external database.
$error_l:=VC_EXT_Close ($error_l)


If ($error_l#0)
	APPEND TO ARRAY(UTIL_ERR_CallChain_at;Current method name)
End if 

$0:=$error_l

