  //%attributes = {"invisible":true,"folder":"VC_Framework Log","lang":"en"} comment added and reserved by 4D.
  // --[HEADERSTART]-------------------------------------
  // User name (OS): Joshua Fletcher
  // Date and time: 04/20/12, 16:52:35
  // ----------------------------------------------------
  // Description:
  //   Process method to handle management of the error log.
  // 
  //   Note this is a log of invalid asset names.
  // --[HEADEREND]---------------------------------------
C_LONGINT($error_l)

C_BOOLEAN($warnNeeded_f)

  // Set up error handling module.
UTIL_ERR_Startup 


  // Clean up any assets that have been fixed already.
  // Note: run this before VC_EXT_Open because it needs to
  // talk to the host database specifically in VCF_EXPORT_FormGet).
If ($error_l=0)
	$error_l:=VC_LOG_Clean 
End if 

  // This is an optimization; most of the following methods require
  // the external database, which means opening and closing the
  // connection over and over.  If I open a connection ahead of time,
  // it will only increment the counter.
$error_l:=VC_EXT_Open 

  // Save the log
If ($error_l=0)
	$error_l:=VC_LOG_Save 
End if 


  // Are there any new assets to warn the user about?
If ($error_l=0)
	$error_l:=VC_LOG_WarnNeeded (->$warnNeeded_f)
End if 


  // Warn the user.
If ($error_l=0)
	If ($warnNeeded_f)
		VC_LOG_ShowWarning 
	End if 
End if 


  // Mark the entries as being warned.
If ($error_l=0)
	$error_l:=VC_LOG_MarkWarned 
End if 


  // Close external database.
$error_l:=VC_EXT_Close ($error_l)


If ($error_l#0)
	UTIL_ERR_HandleError ($error_l;<>VC_ComponentName_t;Current method name;"Error during log export.")
End if 
