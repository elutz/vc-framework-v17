  //%attributes = {"invisible":true,"lang":"en"} comment added and reserved by 4D.
  // --[HEADERSTART]-------------------------------------
  // User name (OS): Joshua Fletcher
  // Date and time: 08/15/13
  // ----------------------------------------------------
  // Description:
  //   A lot of the getters return values that correspond
  //   to 4D constants. In order the make the exported
  //   file more readable, I also export the name of the
  //   constant in addition to just the value.
  // ----------------------------------------------------
  // Changes:
  //   // 11Jan2016 JIH - handle $value_l of -1; setting $value_t to 'unknown'
  // --[HEADEREND]---------------------------------------
C_TEXT($1;$formObjectName_t)
C_TEXT($2;$name_t)
C_OBJECT($3;$formObjectProperties_o)

C_LONGINT($value_l;$dontCare_l;$foregroundColor_l;$backgroundColor_l;$altBackgrndColor_l;$modifiers_l;$maximumValue_l)
C_TEXT($value_t;$labelValue_t;$dontCare_t)
C_POINTER($formObjectPointer_p)
C_BOOLEAN($dontCare_f)

$formObjectName_t:=$1
$name_t:=$2
$formObjectProperties_o:=$3

$value_t:=""  // If this contains data, the attribute value is text.

Case of 
	: ($name_t="type")
		$value_l:=OBJECT Get type(*;$formObjectName_t)
		$maximumValue_l:=Size of array(<>VCF_ObjectTypes_at)
		If (($value_l<=$maximumValue_l) & ($value_l>0))
			$labelValue_t:=<>VCF_ObjectTypes_at{$value_l}
		Else 
			$labelValue_t:="unknown"
		End if 
		
		
	: (($name_t="variableType") | ($name_t="_fieldType"))
		$formObjectPointer_p:=OBJECT Get pointer(Object named;$formObjectName_t)
		$value_l:=Type($formObjectPointer_p->)
		$maximumValue_l:=Size of array(<>VCF_VariableTypes_at)
		If (($value_l<=$maximumValue_l) & ($value_l>0))
			$labelValue_t:=<>VCF_VariableTypes_at{$value_l}
		Else 
			$labelValue_t:="unknown"
		End if 
		
		
	: ($name_t="action")
		  // Action ID's are actually text because, someday, 4D may support custom actions.
		$value_t:=_O_OBJECT Get action(*;$formObjectName_t)
		$maximumValue_l:=Size of array(<>VCF_ObjectActions_at)
		If (($value_l<=$maximumValue_l) & ($value_l>0))
			$labelValue_t:=<>VCF_ObjectActions_at{$value_l}
		Else 
			$labelValue_t:="unknown"
		End if 
		
		
	: ($name_t="borderLineStyle")
		$value_l:=OBJECT Get border style(*;$formObjectName_t)
		$maximumValue_l:=Size of array(<>VCF_BorderLineStyles_at)
		If (($value_l<=$maximumValue_l) & ($value_l>0))
			$labelValue_t:=<>VCF_BorderLineStyles_at{$value_l}
		Else 
			$labelValue_t:="unknown"
		End if 
		
		
	: ($name_t="horizontalAlignment")
		$value_l:=OBJECT Get horizontal alignment(*;$formObjectName_t)
		$maximumValue_l:=Size of array(<>VCF_HorizontalAlign_at)
		If (($value_l<=$maximumValue_l) & ($value_l>0))
			$labelValue_t:=<>VCF_HorizontalAlign_at{$value_l}
		Else 
			$labelValue_t:="unknown"
		End if 
		
		
	: ($name_t="indicatorType")
		$value_l:=OBJECT Get indicator type(*;$formObjectName_t)
		$maximumValue_l:=Size of array(<>VCF_IndicatorType_at)
		If (($value_l<=$maximumValue_l) & ($value_l>0))
			$labelValue_t:=<>VCF_IndicatorType_at{$value_l}
		Else 
			$labelValue_t:="unknown"
		End if 
		
		
	: ($name_t="subformPrintFrame")
		OBJECT GET PRINT VARIABLE FRAME(*;$formObjectName_t;$dontCare_f;$value_l)
		$maximumValue_l:=Size of array(<>VCF_SubformPrintFrame_at)
		If (($value_l<=$maximumValue_l) & ($value_l>0))
			$labelValue_t:=<>VCF_SubformPrintFrame_at{$value_l}
		Else 
			$labelValue_t:="unknown"
		End if 
		
		
	: ($name_t="horizontalSizing")
		OBJECT GET RESIZING OPTIONS(*;$formObjectName_t;$value_l;$dontCare_l)
		$maximumValue_l:=Size of array(<>VCF_HorizontalSizing_at)
		If (($value_l<=$maximumValue_l) & ($value_l>0))
			$labelValue_t:=<>VCF_HorizontalSizing_at{$value_l}
		Else 
			$labelValue_t:="unknown"
		End if 
		
		
	: ($name_t="verticalSizing")
		OBJECT GET RESIZING OPTIONS(*;$formObjectName_t;$dontCare_l;$value_l)
		$maximumValue_l:=Size of array(<>VCF_VerticalSizing_at)
		If (($value_l<=$maximumValue_l) & ($value_l>0))
			$labelValue_t:=<>VCF_VerticalSizing_at{$value_l}
		Else 
			$labelValue_t:="unknown"
		End if 
		
		
	: ($name_t="@groundColor")
		  // The only time this method is called for RGB colors is if negative
		  // values are used, which means 4D constants are being used.
		OBJECT GET RGB COLORS(*;$formObjectName_t;$foregroundColor_l;$backgroundColor_l;$altBackgrndColor_l)
		Case of 
			: ($name_t="foregroundColor")
				$value_l:=$foregroundColor_l
			: ($name_t="backgroundColor")
				$value_l:=$backgroundColor_l
			: ($name_t="altBackgroundColor")
				$value_l:=$altBackgrndColor_l
		End case 
		$maximumValue_l:=Size of array(<>VCF_RGBColors_at)
		If ($value_l<=$maximumValue_l)
			$labelValue_t:=<>VCF_RGBColors_at{Abs($value_l)}
		Else 
			$labelValue_t:="unknown"
		End if 
		
		
	: ($name_t="shortcutKeyModifiers")
		OBJECT GET SHORTCUT(*;$formObjectName_t;$dontCare_t;$value_l)
		$modifiers_l:=$value_l
		If (Int($modifiers_l/Control key mask)=1)
			$labelValue_t:="Control key mask"
			$modifiers_l:=$modifiers_l-Control key mask
		End if 
		If (Int($modifiers_l/Option key mask)=1)
			If ($labelValue_t#"")
				$labelValue_t:=$labelValue_t+"+"
			End if 
			$labelValue_t:=$labelValue_t+"Option key mask"
			$modifiers_l:=$modifiers_l-Option key mask
		End if 
		If (Int($modifiers_l/Shift key mask)=1)
			If ($labelValue_t#"")
				$labelValue_t:=$labelValue_t+"+"
			End if 
			$labelValue_t:=$labelValue_t+"Shift key mask"
			$modifiers_l:=$modifiers_l-Shift key mask
		End if 
		If (Int($modifiers_l/Command key mask)=1)
			If ($labelValue_t#"")
				$labelValue_t:=$labelValue_t+"+"
			End if 
			$labelValue_t:=$labelValue_t+"Command key mask"
		End if 
		
	: ($name_t="verticalAlignment")
		$value_l:=OBJECT Get vertical alignment(*;$formObjectName_t)
		$maximumValue_l:=Size of array(<>VCF_VerticalAlign_at)
		If (($value_l<=$maximumValue_l) & ($value_l>0))
			$labelValue_t:=<>VCF_VerticalAlign_at{$value_l}
		Else 
			$labelValue_t:="unknown"
		End if 
		
		
	Else 
		$name_t:=$name_t+" unknown"
		$value_t:="unknown"
		$labelValue_t:="unknown"
End case 

If ($value_t="")
	OB SET($formObjectProperties_o;$name_t;$value_l)
Else 
	OB SET($formObjectProperties_o;$name_t;$value_t)
End if 

OB SET($formObjectProperties_o;$name_t+"_label";$labelValue_t)
