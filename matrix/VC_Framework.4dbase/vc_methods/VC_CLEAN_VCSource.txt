  //%attributes = {"invisible":true,"lang":"en"} comment added and reserved by 4D.
  // --[HEADERSTART]-------------------------------------
  // User name (OS): Joshua Fletcher
  // Date and time: 07/16/14, 16:24:04
  // ----------------------------------------------------
  // Description:
  //   Previous versions used a folder called "vc_source".
  //
  //   Now using a more accurate name "vc_methods".
  //
  //   This code will preserve previous files.
  //
  //   IMPORTANT: this code *does* support VC integration.
  // --[HEADEREND]---------------------------------------
C_LONGINT($0;$error_l)

C_BOOLEAN($success_f)
C_TEXT($oldFolderPath_t;$newFolderPath_t)

C_TEXT($progTitle_t;$progMessage_t)
C_LONGINT($progID_l;$progCurrentStep_l;$progTotalSteps_l)
C_REAL($progress_r)

$oldFolderPath_t:=Get 4D folder(Database folder;*)+"vc_source"
$newFolderPath_t:=Get 4D folder(Database folder;*)+"vc_methods"

  // See if the old folder exists.
If (Test path name($oldFolderPath_t)=Is a folder)
	
	
	  // Create a progress bar because step 3 can be slow for existing systems.
	If (VC_ProgInstalled_f & (<>VC_Quit_f=False))
		$progTitle_t:=<>VC_ComponentName_t+" migrating previously exported methods"
		$progTotalSteps_l:=3
		$progID_l:=PROG_Create ($progTitle_t;$progTotalSteps_l)
	End if 
	
	
	  // Move the folder first because the VC command may delete it before we get the chance.
	If (VC_ProgInstalled_f & (<>VC_Quit_f=False))
		$progCurrentStep_l:=0
		$progress_r:=$progCurrentStep_l/$progTotalSteps_l
		$progMessage_t:="Renaming folder for exported methods..."
		PROG_Update ($progID_l;$progress_r;$progCurrentStep_l;$progTotalSteps_l;$progMessage_t)
	End if 
	
	$success_f:=UTIL_FOLDER_Move ($oldFolderPath_t;$newFolderPath_t)
	If ($success_f=False)
		$error_l:=<>ERR_CLEAN_MoveFailed_l
	End if 
	
	
	  // Issue VC command to delete the old folder.
	If (VC_ProgInstalled_f & (<>VC_Quit_f=False))
		$progCurrentStep_l:=1
		$progress_r:=$progCurrentStep_l/$progTotalSteps_l
		$progMessage_t:="Deleting old folder from VCS..."
		PROG_Update ($progID_l;$progress_r;$progCurrentStep_l;$progTotalSteps_l;$progMessage_t)
	End if 
	
	If ($error_l=0)
		If (Not(VC_CALL_DEVHOOK_Delete ($oldFolderPath_t)))
			$error_l:=<>ERR_HOOK_DeleteFail_l
		End if 
	End if 
	
	
	  // Issue VC command to add the new folder.
	If (VC_ProgInstalled_f & (<>VC_Quit_f=False))
		$progCurrentStep_l:=2
		$progress_r:=$progCurrentStep_l/$progTotalSteps_l
		$progMessage_t:="Adding new folder to VCS (this may be slow)..."
		PROG_Update ($progID_l;$progress_r;$progCurrentStep_l;$progTotalSteps_l;$progMessage_t)
	End if 
	
	If ($error_l=0)
		If (Not(VC_CALL_DEVHOOK_Create ($newFolderPath_t)))
			$error_l:=<>ERR_HOOK_CreateFail_l
		End if 
	End if 
	
	If (VC_ProgInstalled_f & (<>VC_Quit_f=False))
		$progCurrentStep_l:=3
		$progress_r:=$progCurrentStep_l/$progTotalSteps_l
		$progMessage_t:="Done."
		PROG_Update ($progID_l;$progress_r;$progCurrentStep_l;$progTotalSteps_l;$progMessage_t)
	End if 
	
End if 


If ($error_l#0)
	APPEND TO ARRAY(UTIL_ERR_CallChain_at;Current method name)
End if 


$0:=$error_l
