  //%attributes = {"invisible":true,"lang":"en"} comment added and reserved by 4D.
  // --[HEADERSTART]-------------------------------------
  // User name (OS): Joshua Fletcher
  // Date and time: 07/02/14, 18:06:55
  // ----------------------------------------------------
  // Description:
  //   Find all form changes, log them for later export.
  // --[HEADEREND]---------------------------------------
C_LONGINT($0;$error_l)

C_LONGINT($currentFormStamp_l)

ARRAY TEXT($formsToSave_at;0)
ARRAY LONGINT($formTableNums_al;0)

  // Get the last form stamp.
$currentFormStamp_l:=<>VCF_LastFormStamp_l


  // ==============================================================================
  // Get changed forms since last stamp.
$error_l:=VCF_EXPORT_FormGet (->$currentFormStamp_l;->$formsToSave_at;->$formTableNums_al)


If (($error_l=0) & (Size of array($formsToSave_at)>0))
	  // Optimization since the rest of the methods could open and close a lot.
	$error_l:=VC_EXT_Open 
	
	  // ==============================================================================
	  // Validate form names (invalid paths will be removed).
	If ($error_l=0)
		$error_l:=VCF_NAME_ValidateAll (->$formsToSave_at;->$formTableNums_al)
	End if 
	
	If ($error_l=<>WARN_ASSET_BadPathsFound_l)
		  // This error is ignorable. The user will be warned, but
		  // it shouldn't stop the export.
		$error_l:=0
	End if 
	
	
	  // ==============================================================================
	  // Determine create/update actions.
	If ($error_l=0)
		$error_l:=VCF_EXPORT_FindCreateUpdate (->$formsToSave_at;->$formTableNums_al)
	End if 
	
	  // Close external database.
	$error_l:=VC_EXT_Close ($error_l)
End if 


  // ==============================================================================
  // Determine delete actions.

  // Only run this if something actually changed. This is to minimize
  // the impact of deletion detection because it must check every asset.
  // Note there's a lot of ways the stamp can change (compiling,
  // switching from User to Design, etc.) so this current test may
  // still have too high of an impact. The reason I'm checking the
  // stamp is I verified that the stamp will change when an asset is
  // deleted.
  // If the performance impact is still to high, here's an alternative (but more complex) test.
  // If( ($error_l=0)&(FORM GET NAMES actually returned some forms)
If (($error_l=0) & (<>VCF_LastFormStamp_l#$currentFormStamp_l))
	$error_l:=VCF_EXPORT_FindDelete 
End if 


  // Check to see if the stamp changed and save it if so.
If ($error_l=0)
	If (<>VCF_LastFormStamp_l#$currentFormStamp_l)
		$error_l:=VC_EXT_PrefSet (<>VCF_LABEL_Stamp_t;String($currentFormStamp_l))
		If ($error_l=0)
			  // Only update the stamp if the save was successful.
			<>VCF_LastFormStamp_l:=$currentFormStamp_l
		End if 
	End if 
End if 

If ($error_l#0)
	APPEND TO ARRAY(UTIL_ERR_CallChain_at;Current method name)
End if 

$0:=$error_l
