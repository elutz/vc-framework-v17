  //%attributes = {"invisible":true,"executedOnServer":true,"folder":"VC_Framework Log","lang":"en"} comment added and reserved by 4D.
  // --[HEADERSTART]-------------------------------------
  // User name (OS): Joshua Fletcher
  // Date and time: 04/20/12, 13:35:53
  // ----------------------------------------------------
  // Description:
  //   Remove any paths from the log table and delete the
  //   log file.
  // --[HEADEREND]---------------------------------------
C_LONGINT($0;$error_l)

C_TEXT(<>VC_EXT_LOGFILE_T)

  // Delete the log file.
If (<>VC_EXT_LOGFILE_T#"")
	If (Test path name(<>VC_EXT_LogFile_t)=Is a document)
		DELETE DOCUMENT(<>VC_EXT_LogFile_t)
	End if 
End if 

  // Open external database.
$error_l:=VC_EXT_Open 

If ($error_l=0)
	  // Delete all log entries.
	UTIL_ERR_HandlerInstall 
	Begin SQL
		DELETE FROM VC_PathErrorLog;
	End SQL
	$error_l:=UTIL_ERR_HandlerRemove 
End if 

  // Close external database.
$error_l:=VC_EXT_Close ($error_l)

If ($error_l#0)
	APPEND TO ARRAY(UTIL_ERR_CallChain_at;Current method name)
End if 

$0:=$error_l
